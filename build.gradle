// Top-level build file where you can add configuration options common to all sub-projects/modules.

allprojects {
    group = PROJECTS_GROUP
    version = new Version(VERSION_NAME)
    status = version.status

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'http://dl.bintray.com/optimalorange/maven' }
    }
}

// see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        def isAndroidProject = [
        "com.android.build.gradle.AppPlugin",
        "com.android.build.gradle.LibraryPlugin"
        ].any { it == plugin.class.name }
        if (isAndroidProject) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}


import java.text.SimpleDateFormat
class Version {
    String originalVersion
    String versionName
    int versionCode
    String status
    Date buildTime
    Version(String versionValue) {
        if (versionValue =~ /\d{2,}/) {
            throw new GradleException("versionValue(${versionValue}) contains number more than 9.")
        }
        buildTime = new Date()
        originalVersion = versionValue
        String originalVersionCodeString;
        if (originalVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
            originalVersionCodeString = originalVersion.substring(0, originalVersion.length() - '-SNAPSHOT'.length())
            versionName = originalVersionCodeString + '-' + getTimestamp()
        } else {
            status = 'release'
            versionName = versionValue
            originalVersionCodeString = versionValue
        }
        versionCode = originalVersionCodeString.replace('.', '').toInteger();
    }
    String getTimestamp() {
        // Convert local file timestamp to UTC
        def format = new SimpleDateFormat('yyyyMMddHHmmss')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
        return format.format(buildTime)
    }
    String toString() {
        versionName
    }
}
